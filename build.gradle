plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id 'signing'
  id 'maven-publish'
  id("com.gradle.plugin-publish") version "2.0.0"
  id "com.github.ben-manes.versions" version "0.53.0"
}

group = 'se.alipsa'
version = '2.1.1-SNAPSHOT'
description = 'Automates the release process in Central Portal after signing has completed'

gradlePlugin {
  website.set("https://github.com/Alipsa/nexus-release-plugin")
  vcsUrl.set("https://github.com/Alipsa/nexus-release-plugin")
  plugins {
    simplePlugin {
      id = "${project.group}.${project.name}"
      implementationClass = 'se.alipsa.gradle.plugin.release.NexusReleasePlugin'
      displayName = "Gradle Nexus Release Plugin"
      description = project.description
      tags.set(["publish", "release", "central", "portal", "sonatype"])
    }
  }
}

repositories {
  if (version.contains('SNAPSHOT')) {
    mavenLocal()
  }
  mavenCentral()
}

dependencies {
  def groovyVersion = "5.0.4"
  compileOnly "org.apache.groovy:groovy:${groovyVersion}"

  testImplementation gradleTestKit()
  testImplementation 'org.junit.jupiter:junit-jupiter:6.0.3'
  testImplementation 'org.junit.platform:junit-platform-launcher:6.0.3'
  testImplementation "org.apache.groovy:groovy:${groovyVersion}"
  testImplementation 'com.squareup.okhttp3:mockwebserver:5.3.2'
  testImplementation 'org.testcontainers:testcontainers:2.0.3' // Use latest
  testImplementation 'org.testcontainers:junit-jupiter:1.21.4'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
  withJavadocJar()
  withSourcesJar()
}


publishing {
  publications {
    pluginMaven(MavenPublication) {
      pom {
        name = 'Nexus Release Plugin'
        description = "${project.description}"
        url = "https://github.com/perNyfelt/nexus-release-plugin"
        licenses {
          license {
            name = 'MIT License'
            url = 'https://raw.githubusercontent.com/perNyfelt/nexus-release-plugin/master/LICENSE'
          }
        }
        developers {
          developer {
            id = 'perNyfelt'
            name = 'Per Nyfelt'
          }
        }
        scm {
          url = 'https://github.com/perNyfelt/nexus-release-plugin/tree/master'
          connection = 'scm:git:https://github.com/perNyfelt/nexus-release-plugin.git'
          developerConnection = 'scm:git:https://github.com/perNyfelt/nexus-release-plugin.git'
        }
      }
    }
  }
}

if (project.properties['signing.keyId'] == null) {
  afterEvaluate {
    tasks.named('signPluginMavenPublication').configure {
      enabled = false
    }
    tasks.named('signSimplePluginPluginMarkerMavenPublication').configure {
      enabled = false
    }
  }
}

test {
  useJUnitPlatform()
}

signing {
  if (project.properties['signing.keyId'] != null) {
    project.logger.lifecycle("Signing artifacts...")
    sign publishing.publications.pluginMaven
  } else {
    project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
  }
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}
// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
  gradleReleaseChannel = "current"
  resolutionStrategy {
    componentSelection {
      all {
        if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
          reject('Release candidate')
        }
      }
    }
  }
}
